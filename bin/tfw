#!/usr/bin/env perl
use v5.12;
use warnings;

# ABSTRACT: Command line app for thefuckingweather.com

package
    tfw;

use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
    "tfw %o",
    [ "config=s",   "ini-style configuration file", { default => $ENV{HOME} . '/.tfwrc' } ],
    [ "Celsius|C",  "display temperature in Celsius" ],
    [ "city|c=s",   "city name or zipcode"           ],
    [ "forecast|f", "display forecast"               ],
    [ "help|h|?",   "display help"                   ],
);

print $usage->text and exit if $opt->help;

if ( -f $opt->config ) {

    my $config = read_config( $opt->config );

    foreach my $key (keys %$config) {
        $opt->{$key} = $config->{$key} unless defined $opt->{$key};
    }
}

die "Required option missing: city\n" unless $opt->city;

require  WWW::Scraper::TFW;
my $tfw = WWW::Scraper::TFW->new( city => $opt->city, celsius => $opt->celsius );

say $tfw->temperature;
say $tfw->remark;

say "\n" . format_forecast( $tfw->forecast ) if $opt->forecast;

sub format_forecast {
    my $forecast = shift;

    require Text::Table;

    my @columns = qw(day high low weather);

    my $table = Text::Table->new(@columns);

    $table->load( map { [ @{$_}{@columns} ] } @$forecast );

    return $table;
}

sub read_config {
    my $file = shift;

    require Config::INI::Reader;

    my $config = Config::INI::Reader->read_file( $opt->config )
      or die "Couldn't read configuration file " . $opt->config . "\n";

    # Only use main section of ini file
    return $config->{_};
}

=head1 DESCRIPTION

This is a command line wrapper around L<WWW::Scraper::TFW>. It outputs
weather information from L<thefuckingweather.com>.

=cut

=head1 SYNOPSIS

    ~$ tfw -c 'La Plata'
    20ยบ?! ITS FUCKING NICE!
    Enjoy.

You can save your preferences in an ini-style configuration file. In
$HOME/.tfwrc:

    city     = La Plata

    ; These will be evaluated in boolean context
    Celsius  = fuck yes
    forecast = yes

And then you can just do:

    ~$ tfw
    21ยบ?! ITS FUCKING NICE!
    uh, can we trade?

    day high low weather
    Mon 22   13  Clear
    Tue 24   13  Sunny

=cut

=head1 OPTIONS

    --config        ini-style configuration file
    -C --Celsius    display temperature in Celsius
    -c --city       city name or zipcode
    -f --forecast   display forecast
    -? -h --help    display help

=cut

1;
